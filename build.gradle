buildscript {
	ext {
		springBootVersion = '1.5.9.RELEASE'
	}
	repositories {
		mavenCentral()
		jcenter()
		maven {
			url "https://plugins.gradle.org/m2/"
		}
	}
	dependencies {
		classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
		classpath "io.spring.gradle:dependency-management-plugin:0.6.0.RELEASE"
		classpath "gradle.plugin.com.ewerk.gradle.plugins:querydsl-plugin:1.0.7"
	}
}

ext {
	projectGroup = 'com.jojoldu.dev-beginner-group'
	projectVersion = '0.0.8'
}

task wrapper(type: Wrapper) {
	gradleVersion = '4.3.1'
}

subprojects {
	apply plugin: 'java'
	apply plugin: 'groovy'
	apply plugin: 'eclipse'
	apply plugin: 'org.springframework.boot'
	apply plugin: "com.ewerk.gradle.plugins.querydsl"

	group = "${projectGroup}"
	version = "${projectVersion}"
	sourceCompatibility = 1.8

	repositories {
		mavenCentral()
		jcenter()
	}

	//Spring Boot Overriding
	ext['hibernate.version'] = '5.2.11.Final'
	ext['hikaricp.version'] = '2.7.1'

	springBoot {
		buildInfo {
			additionalProperties = [
					'group'         : "${projectGroup}",
					'version'       : "${projectVersion}",
					'build-datetime': new Date().format("yyyy-MM-dd HH:mm:ss zzz")
			]
		}
	}

	def querydslSrcDir = 'src/main/generated'

	querydsl {
		library = "com.querydsl:querydsl-apt"
		jpa = true
		querydslSourcesDir = querydslSrcDir
	}

	sourceSets {
		main {
			java {
				srcDirs = ['src/main/java', querydslSrcDir]
			}
		}
	}

	dependencies {
		compile('org.springframework.boot:spring-boot-starter')
		compile("org.springframework.boot:spring-boot-starter-actuator")
		compile('org.apache.commons:commons-lang3:3.7')
		compile 'com.google.code.findbugs:jsr305:3.0.2'
		compile('org.projectlombok:lombok')
		testCompile('org.springframework.boot:spring-boot-starter-test')
		testCompile('org.spockframework:spock-core:1.1-groovy-2.4')
		testCompile('org.spockframework:spock-spring:1.1-groovy-2.4')
	}
}



